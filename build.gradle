plugins {
  id 'java'
  id 'jacoco'
  id 'com.diffplug.spotless' version '8.0.0'
  id 'org.cthing.locc' version '2.1.0'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri('https://gitlab.di.unimi.it/api/v4/projects/7715/packages/maven')
  }
}

configurations {
  umlDoclet
}

dependencies {
  testImplementation 'it.unimi.di.prog2:jubbiot:0.4a0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  umlDoclet 'nl.talsmasoftware:umldoclet:2.2.0'
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.compilerArgs.add('-Xlint:all')
  options.compilerArgs.add('-Werror') 
}

tasks.withType(JavaExec) {
  jvmArgs += ['-Duser.language=ROOT', '-Dfile.encoding=UTF-8']
}

tasks.withType(Test) {
  jvmArgs += ['-Duser.language=ROOT', '-Dfile.encoding=UTF-8']
}

tasks.named('test') {
  useJUnitPlatform()
  testLogging {
    events 'skipped', 'failed'
    afterSuite { desc, result ->
      if (!desc.parent) {
        println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
  }
  defaultCharacterEncoding = 'UTF-8'
  finalizedBy jacocoTestReport
}

tasks.withType(JavaExec) {
  enableAssertions = !providers.systemProperty('disableAssertions').isPresent()
}

tasks.register('runClass', JavaExec) {
  dependsOn build
  group = 'Execution'
  description = 'Runs the main method of a class given its fully qualified name.'
  mainClass = providers.systemProperty('mainClass').getOrElse('my.pkg.Example')
  classpath = sourceSets.main.runtimeClasspath
  defaultCharacterEncoding = 'UTF-8'
}

javadoc {
  exclude('utils/**', 'clients/**')
  options {
    description = 'Generates Javadoc API documentation and UML diagrams for the main source code.'
    encoding = 'UTF-8'
    charSet = 'UTF-8'
    links = [ 'https://docs.oracle.com/en/java/javase/25/docs/api/' ]
    docTitle = 'Handouts â€” Programmazione II @ UniMI'
    overview = 'src/overview.html'
    header = '<script type=\'text/javascript\' src=\'https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML\'></script>'
    docletpath = configurations.umlDoclet.files.asType(List)
    doclet = 'nl.talsmasoftware.umldoclet.UMLDoclet'
    showFromPrivate()
    addBooleanOption 'Xdoclint:all', true
    addBooleanOption 'Werror', true
    addBooleanOption '-allow-script-in-comments', true
  }
}

spotless {
  enforceCheck = false
  java {
    googleJavaFormat('1.28.0')
    removeUnusedImports()
    leadingTabsToSpaces(2)
    endWithNewline()
    licenseHeaderFile 'src/licenseHeaderFile.txt'
  }
}

tasks.register('cleanActual', Delete) {
  delete fileTree('tests').matching {
    include '**/actual-*.txt'
  }
}

clean {
  dependsOn cleanActual
}
